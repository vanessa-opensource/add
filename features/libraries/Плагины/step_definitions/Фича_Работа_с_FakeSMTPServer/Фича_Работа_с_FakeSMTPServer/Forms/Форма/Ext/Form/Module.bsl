
#Область Служебные_функции_и_процедуры

&НаКлиенте
// контекст фреймворка Vanessa-ADD
Перем Ванесса;

&НаКлиенте
// Структура, в которой хранится состояние сценария между выполнением шагов. Очищается перед выполнением каждого сценария.
Перем Контекст Экспорт;

&НаКлиенте
// Структура, в которой можно хранить служебные данные между запусками сценариев. Существует, пока открыта форма Vanessa-ADD.
Перем КонтекстСохраняемый Экспорт;

&НаКлиенте
// Функция экспортирует список шагов, которые реализованы в данной внешней обработке.
Функция ПолучитьСписокТестов(КонтекстФреймворкаBDD) Экспорт
	Ванесса = КонтекстФреймворкаBDD;

	ВсеТесты = Новый Массив;

	// описание шагов
	// пример вызова Ванесса.ДобавитьШагВМассивТестов(ВсеТесты, Снипет, ИмяПроцедуры, ПредставлениеТеста, ОписаниеШага, ТипШагаДляОписания, ТипШагаВДереве);

	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ПортЯщика(Парам01)",					"ПортЯщика",				"Допустим порт ящика '1080'",						"Установка порта, по которому расположен FakeSMTPServer",					"Плагины.МокПочта");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"АдресЯщика(Парам01)",				"АдресЯщика",				"Допустим адрес ящика '0.0.0.0'",					"Установка порта, по которому расположен FakeSMTPServer",					"Плагины.МокПочта");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ПериодОпросаПочтыСек(Парам01)",		"ПериодОпросаПочтыСек",		"Допустим период опроса почты '1' сек",				"Установка периода времени, в течении которого письмо считается НОВЫМ",		"Плагины.МокПочта");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ОчищаемЯщикОтПисем()",				"ОчищаемЯщикОтПисем",		"Тогда очищаем ящик от писем",						"Удаляет все письма из ящика",												"Плагины.МокПочта");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ВЯщикеЕстьПисьма()",					"ВЯщикеЕстьПисьма",			"Тогда в ящике есть письма",						"Проверяет, что есть письма в ящике и сохраняет их в переменную Контекст",	"Плагины.МокПочта");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ВЯщикеЕстьНовыеПисьма()",			"ВЯщикеЕстьНовыеПисьма",	"Тогда в ящике есть новые письма",					"Проверяет, что есть новые письма и сохраняет их в переменную Контекст",	"Плагины.МокПочта");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ВЯщикеЕстьПисьмаСТемой(Парам01)",	"ВЯщикеЕстьПисьмаСТемой",	"Тогда в ящике есть письма с темой ""Тема""",		"Проверяет, что есть письма с указанными словами в Теме",					"Плагины.МокПочта");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ВЯщикеЕстьПисьмаСТекстом(Парам01)",	"ВЯщикеЕстьПисьмаСТекстом",	"Тогда в ящике есть письма с текстом ""Текст""",	"Проверяет, что есть письма с указанными словами в Тексте",					"Плагины.МокПочта");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ВЯщикеЕстьПисьмаОт(Парам01)",		"ВЯщикеЕстьПисьмаОт",		"Тогда в ящике есть письма от ""Отправитель""",		"Проверяет, что есть пиьсма от определенного адреса",						"Плагины.МокПочта");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ВЯщикеЕстьПисьмаДля(Парам01)",		"ВЯщикеЕстьПисьмаДля",		"Тогда в ящике есть письма для ""Получатель""",		"Проверяет, что есть письма для определенного адреса",						"Плагины.МокПочта");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ТемойПисьма(Парам01)",				"ТемойПисьма",				"И темой письма ""Тема""",							"Используется для уточения писем по Теме",									"Плагины.МокПочта");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ТекстомПисьма(Парам01)",				"ТекстомПисьма",			"И текстом письма ""Текст""",						"Используется для уточения писем по Тексту",								"Плагины.МокПочта");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ПисьмаОт(Парам01)",					"ПисьмаОт",					"И письма от ""Отправитель""",						"Используется для уточения писем по отправителю",							"Плагины.МокПочта");
	Ванесса.ДобавитьШагВМассивТестов(ВсеТесты,"ПисьмаДля(Парам01)",					"ПисьмаДля",				"И письма для ""Получатель""",						"Используется для уточения писем по получателю",							"Плагины.МокПочта");

	Возврат ВсеТесты;
КонецФункции

&НаСервере
// Служебная функция.
Функция ПолучитьМакетСервер(ИмяМакета)
	ОбъектСервер = РеквизитФормыВЗначение("Объект");
	Возврат ОбъектСервер.ПолучитьМакет(ИмяМакета);
КонецФункции

&НаКлиенте
// Служебная функция для подключения библиотеки создания fixtures.
Функция ПолучитьМакетОбработки(ИмяМакета) Экспорт
	Возврат ПолучитьМакетСервер(ИмяМакета);
КонецФункции

#КонецОбласти



#Область Работа_со_сценариями

&НаКлиенте
// Процедура выполняется перед началом каждого сценария
Процедура ПередНачаломСценария() Экспорт

КонецПроцедуры

&НаКлиенте
// Процедура выполняется перед окончанием каждого сценария
Процедура ПередОкончаниемСценария() Экспорт

КонецПроцедуры

#КонецОбласти



#Область Вспомогательные_процедуры

&НаКлиенте
Процедура ИнициализироватьПараметрыFakeMail()

	Если НЕ КонтекстСохраняемый.Свойство("FakeMail") Тогда
		ТиповаяСтруктураНастроек = Новый Структура;
		ТиповаяСтруктураНастроек.Вставить("Address",	"localhost");
		ТиповаяСтруктураНастроек.Вставить("Port",		1080);
		ТиповаяСтруктураНастроек.Вставить("Timeout",	5);

		КонтекстСохраняемый.Вставить("FakeMail", ТиповаяСтруктураНастроек);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НастроитьПодключениеКЯщику()
	Если НЕ Контекст.Свойство("СоединениеFakeMail") Тогда

		HTTPСоед = Новый HTTPСоединение(ПолучитьАдресЯщика(),
										ПолучитьПортЯщика());

		Контекст.Вставить("СоединениеFakeMail", HTTPСоед);

	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ПолучитьПисьма(ОтКого = "", ДляКого = "", СДаты = Неопределено, ПоДату = Неопределено)
	ИнициализироватьПараметрыFakeMail();
	НастроитьПодключениеКЯщику();

	АдресЗапроса = "/api/emails";
	Разделитель = "?";

	Если ЗначениеЗаполнено(ОтКого) Тогда
		АдресЗапроса	= АдресЗапроса + Разделитель + "from="	+ ОтКого;
		Разделитель		= "&";
	КонецЕсли;

	Если ЗначениеЗаполнено(ДляКого) Тогда
		АдресЗапроса	= АдресЗапроса + Разделитель + "to="	+ ДляКого;
		Разделитель		= "&";
	КонецЕсли;

	Если ЗначениеЗаполнено(СДаты) Тогда
		АдресЗапроса	= АдресЗапроса + Разделитель + "since="	+ Формат(СДаты, "ДФ=yyyy-MM-ddTHH:mm:ssZ");
		Разделитель		= "&";
	КонецЕсли;

	Если ЗначениеЗаполнено(ПоДату) Тогда
		АдресЗапроса	= АдресЗапроса + Разделитель + "until="	+ Формат(ПоДату, "ДФ=yyyy-MM-ddTHH:mm:ssZ");
		Разделитель		= "&";
	КонецЕсли;

	ReqGet = Новый HTTPЗапрос(АдресЗапроса);
	HTTPРезультат = Контекст.СоединениеFakeMail.Получить(ReqGet);


	Возврат HTTPРезультат;
КонецФункции

&НаКлиенте
Процедура ВалидироватьРезультатЗапросаПисем(HTTPРезультат)
	Ванесса.ПроверитьРавенство(HTTPРезультат.КодСостояния, 200);

	ТекстОтвета = HTTPРезультат.ПолучитьТелоКакСтроку();

	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(ТекстОтвета);
	Результат	= ПрочитатьJSON(ЧтениеJSON, Истина, "date", ФорматДатыJSON.ISO);

	Ванесса.ПроверитьТип(Результат, Тип("Массив"));
	Ванесса.ПроверитьБольше(Результат.Количество(), 0, "В ящике должны быть письма");

	Контекст.Вставить("FakeEmails", Результат);
КонецПроцедуры

&НаКлиенте
Процедура ОтфильтроватьПисьмаПоПолю(Поле, Значение)
	Письма = Контекст.FakeEmails;

	Сч = 0;
	Пока Сч < Письма.Количество() Цикл
		Письмо = Письма.Получить(Сч);

		Если Найти(Письмо[Поле], Значение) = 0 Тогда
			Письма.Удалить(Сч);
		Иначе
			Сч = Сч + 1;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОтфильтроватьПисьмаПоПолюОт(Отправитель)
	Письма = Контекст.FakeEmails;

	Сч = 0;
	Пока Сч < Письма.Количество() Цикл
		Письмо = Письма.Получить(Сч);

		КорректныйОтправитель = Ложь;

		СписокОтправитлей = Письмо["from"]["value"];
		Для Каждого ОтправительПиьсма из СписокОтправитлей Цикл

			Если Найти(ОтправительПиьсма["address"], Отправитель) <> 0 Тогда

				КорректныйОтправитель = Истина;
				Прервать;

			КонецЕсли;

		КонецЦикла;

		Если Не КорректныйОтправитель Тогда
			Письма.Удалить(Сч);
		Иначе
			Сч = Сч + 1;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ОтфильтроватьПисьмаПоПолюКому(Получатель)
	Письма = Контекст.FakeEmails;

	Сч = 0;
	Пока Сч < Письма.Количество() Цикл
		Письмо = Письма.Получить(Сч);

		КорректныйПолучатель = Ложь;

		СписокПолучателей = Письмо["to"]["value"];
		Для Каждого ПолучательПиьсма из СписокПолучателей Цикл

			Если Найти(ПолучательПиьсма["address"], Получатель) <> 0 Тогда

				КорректныйПолучатель = Истина;
				Прервать;

			КонецЕсли;

		КонецЦикла;

		Если Не КорректныйПолучатель Тогда
			Письма.Удалить(Сч);
		Иначе
			Сч = Сч + 1;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область Получение_настроек

&НаКлиенте
Функция ПолучитьАдресЯщика()
	Возврат КонтекстСохраняемый.FakeMail.Address;
КонецФункции

&НаКлиенте
Функция ПолучитьПортЯщика()
	Возврат КонтекстСохраняемый.FakeMail.Port;
КонецФункции

&НаКлиенте
Функция ПолучитьТаймАут()
	Возврат КонтекстСохраняемый.FakeMail.Timeout;
КонецФункции

#КонецОбласти

///////////////////////////////////////////////////
//Реализация шагов
///////////////////////////////////////////////////

&НаКлиенте
//Допустим порт ящика '1080'
//@ПортЯщика(Парам01)
Процедура ПортЯщика(Парам01) Экспорт
	ИнициализироватьПараметрыFakeMail();
	КонтекстСохраняемый.FakeMail.Port = Число(Парам01);
КонецПроцедуры

&НаКлиенте
//Допустим адрес ящика '0.0.0.0'
//@АдресЯщика(Парам01)
Процедура АдресЯщика(Парам01) Экспорт
	ИнициализироватьПараметрыFakeMail();
	КонтекстСохраняемый.FakeMail.Address = Парам01;
КонецПроцедуры

&НаКлиенте
//Допустим период опроса почты '1' сек
//@ПериодОпросаПочтыСек(Парам01)
Процедура ПериодОпросаПочтыСек(Парам01) Экспорт
	ИнициализироватьПараметрыFakeMail();
	КонтекстСохраняемый.FakeMail.Timeout = Число(Парам01);
КонецПроцедуры

&НаКлиенте
//Тогда очищаем ящик от писем
//@ОчищаемЯщикОтПисем()
Процедура ОчищаемЯщикОтПисем() Экспорт
	ИнициализироватьПараметрыFakeMail();
	НастроитьПодключениеКЯщику();

	ReqDel = Новый HTTPЗапрос("/api/emails");
	HTTPРезультат = Контекст.СоединениеFakeMail.Удалить(ReqDel);
	Ванесса.ПроверитьРавенство(HTTPРезультат.КодСостояния, 200);
КонецПроцедуры

&НаКлиенте
//Тогда в ящике есть письма
//@ВЯщикеЕстьПисьма()
Процедура ВЯщикеЕстьПисьма() Экспорт
	HTTPРезультат = ПолучитьПисьма();

	ВалидироватьРезультатЗапросаПисем(HTTPРезультат);
КонецПроцедуры

&НаКлиенте
//Тогда в ящике есть новые письма
//@ВЯщикеЕстьНовыеПисьма()
Процедура ВЯщикеЕстьНовыеПисьма() Экспорт
	ИнициализироватьПараметрыFakeMail();

	ТекДата = УниверсальноеВремя(ТекущаяДата());
	Начало	= ТекДата - ПолучитьТаймАут();
	Конец	= ТекДата + ПолучитьТаймАут();

	HTTPРезультат = ПолучитьПисьма(,,Начало, Конец);
	ВалидироватьРезультатЗапросаПисем(HTTPРезультат);
КонецПроцедуры

&НаКлиенте
//Тогда в ящике есть письма с темой "Тема"
//@ВЯщикеЕстьПисьмаСТемой(Парам01)
Процедура ВЯщикеЕстьПисьмаСТемой(Парам01) Экспорт
	HTTPРезультат = ПолучитьПисьма();
	ВалидироватьРезультатЗапросаПисем(HTTPРезультат);

	ОтфильтроватьПисьмаПоПолю("subject", Парам01);
	Ванесса.ПроверитьБольше(Контекст.FakeEMails, 0);
КонецПроцедуры

&НаКлиенте
//Тогда в ящике есть письма с текстом "Текст"
//@ВЯщикеЕстьПисьмаСТекстом(Парам01)
Процедура ВЯщикеЕстьПисьмаСТекстом(Парам01) Экспорт
	HTTPРезультат = ПолучитьПисьма();
	ВалидироватьРезультатЗапросаПисем(HTTPРезультат);

	ОтфильтроватьПисьмаПоПолю("text", Парам01);
	Ванесса.ПроверитьБольше(Контекст.FakeEMails, 0);
КонецПроцедуры

&НаКлиенте
//Тогда в ящике есть письма от "Отправитель"
//@ВЯщикеЕстьПисьмаОт(Парам01)
Процедура ВЯщикеЕстьПисьмаОт(Парам01) Экспорт
	HTTPРезультат = ПолучитьПисьма(Парам01);

	ВалидироватьРезультатЗапросаПисем(HTTPРезультат);
КонецПроцедуры

&НаКлиенте
//Тогда в ящике есть письма для "Получатель"
//@ВЯщикеЕстьПисьмаДля(Парам01)
Процедура ВЯщикеЕстьПисьмаДля(Парам01) Экспорт
	HTTPРезультат = ПолучитьПисьма(,Парам01);

	ВалидироватьРезультатЗапросаПисем(HTTPРезультат);
КонецПроцедуры

&НаКлиенте
//И темой письма "Тема"
//@ТемойПисьма(Парам01)
Процедура ТемойПисьма(Парам01) Экспорт
	ОтфильтроватьПисьмаПоПолю("subject", Парам01);
	Ванесса.ПроверитьБольше(Контекст.FakeEMails.Количество(), 0);
КонецПроцедуры

&НаКлиенте
//И текстом письма "Текст"
//@ТекстомПисьма(Парам01)
Процедура ТекстомПисьма(Парам01) Экспорт
	ОтфильтроватьПисьмаПоПолю("text", Парам01);
	Ванесса.ПроверитьБольше(Контекст.FakeEMails.Количество(), 0);
КонецПроцедуры

&НаКлиенте
//И письма от "Отправитель"
//@ПисьмаОт(Парам01)
Процедура ПисьмаОт(Парам01) Экспорт
	ОтфильтроватьПисьмаПоПолюОт(Парам01);
	Ванесса.ПроверитьБольше(Контекст.FakeEMails.Количество(), 0);
КонецПроцедуры

&НаКлиенте
//И письма для "Получатель"
//@ПисьмаДля(Парам01)
Процедура ПисьмаДля(Парам01) Экспорт
	ОтфильтроватьПисьмаПоПолюКому(Парам01);
	Ванесса.ПроверитьБольше(Контекст.FakeEMails.Количество(), 0);
КонецПроцедуры
