# Данные 

- [Данные](#%D0%B7%D0%B0%D0%B3%D0%BB%D1%83%D1%88%D0%BA%D0%B8)
  - [Общее описание программного интерфейса](#%D0%BE%D0%B1%D1%89%D0%B5%D0%B5-%D0%BE%D0%BF%D0%B8%D1%81%D0%B0%D0%BD%D0%B8%D0%B5-%D0%BF%D1%80%D0%BE%D0%B3%D1%80%D0%B0%D0%BC%D0%BC%D0%BD%D0%BE%D0%B3%D0%BE-%D0%B8%D0%BD%D1%82%D0%B5%D1%80%D1%84%D0%B5%D0%B9%D1%81%D0%B0)
    - [Программный интерфейс v1](#%D0%BF%D1%80%D0%BE%D0%B3%D1%80%D0%B0%D0%BC%D0%BC%D0%BD%D1%8B%D0%B9-%D0%B8%D0%BD%D1%82%D0%B5%D1%80%D1%84%D0%B5%D0%B9%D1%81-v1)
    - [Программный интерфейс v2](#%D0%BF%D1%80%D0%BE%D0%B3%D1%80%D0%B0%D0%BC%D0%BC%D0%BD%D1%8B%D0%B9-%D0%B8%D0%BD%D1%82%D0%B5%D1%80%D1%84%D0%B5%D0%B9%D1%81-v2)
  - [Примеры](#%D0%BF%D1%80%D0%B8%D0%BC%D0%B5%D1%80%D1%8B)
    - [Пример создание простого элемента справочника](#%D0%BF%D1%80%D0%B8%D0%BC%D0%B5%D1%80-%D1%81%D0%BE%D0%B7%D0%B4%D0%B0%D0%BD%D0%B8%D0%B5-%D0%BF%D1%80%D0%BE%D1%81%D1%82%D0%BE%D0%B3%D0%BE-%D1%8D%D0%BB%D0%B5%D0%BC%D0%B5%D0%BD%D1%82%D0%B0-%D1%81%D0%BF%D1%80%D0%B0%D0%B2%D0%BE%D1%87%D0%BD%D0%B8%D0%BA%D0%B0)
    - [Пример создания Документа](#%D0%BF%D1%80%D0%B8%D0%BC%D0%B5%D1%80-%D1%81%D0%BE%D0%B7%D0%B4%D0%B0%D0%BD%D0%B8%D1%8F-%D0%B4%D0%BE%D0%BA%D1%83%D0%BC%D0%B5%D0%BD%D1%82%D0%B0)
    - [Пример создания Набора записей регистра накопления](#%D0%BF%D1%80%D0%B8%D0%BC%D0%B5%D1%80-%D1%81%D0%BE%D0%B7%D0%B4%D0%B0%D0%BD%D0%B8%D1%8F-%D0%BD%D0%B0%D0%B1%D0%BE%D1%80%D0%B0-%D0%B7%D0%B0%D0%BF%D0%B8%D1%81%D0%B5%D0%B9-%D1%80%D0%B5%D0%B3%D0%B8%D1%81%D1%82%D1%80%D0%B0-%D0%BD%D0%B0%D0%BA%D0%BE%D0%BF%D0%BB%D0%B5%D0%BD%D0%B8%D1%8F)
    - [Пример создания таблицы значений](#%D0%BF%D1%80%D0%B8%D0%BC%D0%B5%D1%80-%D1%81%D0%BE%D0%B7%D0%B4%D0%B0%D0%BD%D0%B8%D1%8F-%D1%82%D0%B0%D0%B1%D0%BB%D0%B8%D1%86%D1%8B-%D0%B7%D0%BD%D0%B0%D1%87%D0%B5%D0%BD%D0%B8%D0%B9)

Плагин "Данные" предназначен для создания объектов в БД минимально необходимых для проверки функции/процедуры. Т.е. у объекта будут заполнены только те поля, которые вы посчитаете необходимыми для прохождения тестов функции или процедуры.

## Общее описание программного интерфейса

Особенности создания объектов:

* Все объекты создаются в режиме Загрузка.ОбменДанными = Истина
* Для справочников, если не указан реквизит "Наименование", то Наименование присваивается автоматически по правилу: "Наименование_" + случайная последовательность символов (Новый УникальныйИдентификатор)
* Для регистров накопления и регистров сведений наличие в наименовании параметра объекта ключевого слова _Отбор устанавливает отбор по набору записей
* Если ссылочный реквизит объекта объявлен, но значение не задано (Неопределено), то плагин автоматически создаст пустой объект

```bsl
// У справочника "Складские ячейки" владелец справочник "Склады"
// Тогда функция следующего вида вернет ссылку на элемент справочника, где Ячейка.Владелец - это созданный в тесте новый элемент справочника "Склады"
Ячейка = Данные.СоздатьЭлементСправочника("СкладскиеЯчейки", Новый Структура("Владелец"));
```

Плагин содержит две версии интерфейса для создание объекта:

- функции для создание метаданных и описание объектов через структуры (v1)
- функции "НачатьСоздание" и "Создать" с применением текучих выражений (v2)

Интерфейс v1 подходит для быстрого создания простых объектов с заполнением одного, максимум двух реквизитов

Интерфейс v2 подходит для описание более сложных объектов

### Программный интерфейс v1

Для описание объектов используются структуры, которые потом передаются в методы создания объектов.

Поддерживаются следующие методы для создания объектов:

* СоздатьПланОбмена
* СоздатьЭлементСправочника
* СоздатьДокумент
* НачатьВводТабличнойЧасти
* НачатьВводНабораЗаписей
* СтрокаНабора
* СоздатьНаборЗаписей
* СоздатьПланВидовХарактеристик

Для описание табличной части используются текучие выражения и методы:

* НачатьВводТабличнойЧасти - описывает шапку таблицы, поддерживает до 10 столбцов
* СтрокаТЧ - описывает строку таблицы, поддерживает до 10 столбцов

Пример теста, в котором создается документ и проверяется замена ячеек после записи:

```bsl
// I получение данных из макета
	Склад = Данные.СоздатьЭлементСправочника("Склады");
	Склад2 = Данные.СоздатьЭлементСправочника("Склады");
	
	ЯчейкаНеправильная = Данные.СоздатьЭлементСправочника("СкладскиеЯчейки", Новый Структура("Владелец, Наименование, Код", Склад2, "Яч1_Тест", "1"));
	ЯчейкаПравильная = Данные.СоздатьЭлементСправочника("СкладскиеЯчейки", Новый Структура("Владелец, Наименование, Код", Склад, "Яч1_Тест", "2"));
	Номенклатура = Данные.СоздатьЭлементСправочника("Номенклатура");
	
	ПараметрыДок = Новый Структура;
	ПараметрыДок.Вставить("Склад",Склад);
	
	Данные.НачатьВводТабличнойЧасти(ПараметрыДок,
			"ТоварыОтбор","Ячейка")
	        .СтрокаТЧ(ЯчейкаНеправильная)
			.СтрокаТЧ(ЯчейкаПравильная);
			
	Данные.НачатьВводТабличнойЧасти(ПараметрыДок,
			"ТоварыРазмещение","Ячейка")
	        .СтрокаТЧ(ЯчейкаНеправильная)
			.СтрокаТЧ(ЯчейкаПравильная);
			
	// II подготовка к вызову и вызов проверяемой функции
	Док = Данные.СоздатьДокумент("ОтборРазмещениеТоваров", ПараметрыДок);

	// III обработка и проверка результата
	Утверждения.ПроверитьРавенство(Док.ТоварыОтбор[0].Ячейка, Док.ТоварыОтбор[1].Ячейка);
	Утверждения.ПроверитьРавенство(Док.ТоварыРазмещение[0].Ячейка, Док.ТоварыРазмещение[1].Ячейка);
```

### Программный интерфейс v2

Для описания объектов используются методы "НачатьСоздание" и "Создать".

С помощью методов:

* Реквизит
* ШапкаТаблицы
* СтрокаТЧ
* ШапкаНабора (для Наборов записей)
* СтрокаНабора (для Наборов записей)

описываются объекты, например так:

```bsl
Данные.НачатьСоздание("Документ.ДокументСДвижениями")
		.Реквизит("РеквизитПростойСправочник")
		.ШапкаТабличнойЧасти("ТЧ","Реквизит1", "РесурсЧисло")
			.СтрокаТЧ("Элемент1", 10)
			.СтрокаТЧ("Элемент2", 15);
	Док = Данные.Создать();
```

## Примеры

### Пример создание простого элемента справочника

Старый интерфейс:

```bsl
ПараметрыСправочника = Новый Структура;
	ПараметрыСправочника.Вставить("Реквизит1",Перечисления.Перечисление1.ЗначениеПеречисления1);
	ПараметрыСправочника.Вставить("РеквизитСтрока","Строка");
	Данные.НачатьВводТабличнойЧасти(ПараметрыСправочника, "ТабличнаяЧасть1",
		"РеквизитЧисло", "РеквизитДата")
	.СтрокаТЧ(10, Дата(2,1,1))
	.СтрокаТЧ(20, Дата(3,1,1));

```

Новый интерфейс:

```bsl
Данные.НачатьСоздание("Справочник.Справочник1")
	.Реквизит("Реквизит1", Перечисления.Перечисление1.ЗначениеПеречисления1)
	.Реквизит("РеквизитСтрока", "Строка")
	.ШапкаТабличнойЧасти("ТабличнаяЧасть1",
		"РеквизитЧисло", "РеквизитДата")
		.СтрокаТЧ(10, Дата(2,1,1))
		.СтрокаТЧ(20, Дата(3,1,1));
	ЭлементСправочника = Данные.Создать();
```

### Пример создания Документа

Старый интерфейс:

```bsl
ПараметрыДокумента = Новый Структура;
	ПараметрыДокумента.Вставить("РеквизитПростойСправочник");
	Данные.НачатьВводТабличнойЧасти(ПараметрыДокумента, "ТЧ",
		,"Реквизит1", "РесурсЧисло")
			.СтрокаТЧ("Элемент1", 10)
			.СтрокаТЧ("Элемент2", 15);
	Док = Данные.СоздатьДокумент("ДокументСДвижениями", ПараметрыДокумента);
```

Новый интерфейс:

```bsl
Данные.НачатьСоздание("Документ.ДокументСДвижениями")
		.Реквизит("РеквизитПростойСправочник")
		.ШапкаТабличнойЧасти("ТЧ","Реквизит1", "РесурсЧисло")
			.СтрокаТЧ("Элемент1", 10)
			.СтрокаТЧ("Элемент2", 15);
	Док = Данные.Создать();
```

### Пример создания Набора записей регистра накопления

Новый интерфейс:

```bsl
Данные.НачатьСоздание("РегистрНакопления.РегистрНакопленияОстатки")
        // Ключевое слово _Отбор устанавливает отбор по набору записей
		.Реквизит("_ОтборРегистратор", Данные.СоздатьДокумент("ДокументСДвижениями"))
		.Реквизит("Период", НачалоДня(ТекущаяДата()))
		.Реквизит("ВидДвижения", ВидДвиженияНакопления.Приход)
		.ШапкаНабора("Измерение1", "РесурсЧисло1")
			.ЗаписьНабора("Тестовое1", 100)
			.ЗаписьНабора("Тестовое2", 120);
	Набор = Данные.Создать();
```

### Пример создания таблицы значений

Новый интерфейс:

```bsl
Данные.НачатьСоздание("Таблица")
		.ШапкаТабличнойЧасти("Собственная","Реквизит1", "РесурсЧисло")
			.СтрокаТЧ("Элемент1", 10)
			.СтрокаТЧ("Элемент2", 15);
ТаблицаЗначений = Данные.ТаблицаЗначений("Собственная");
```