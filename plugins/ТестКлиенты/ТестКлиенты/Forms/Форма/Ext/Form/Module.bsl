&НаКлиенте
Перем ЗапущенныеТестКлиенты;

&НаКлиенте
Перем КонтекстЯдра;

&НаКлиенте
Перем ПортПоУмолчанию;

&НаКлиенте
Перем НастройкиМодальныхОкон;

&НаКлиенте
Перем ЗаголовкиМодальныхОкон;

&НаКлиенте
Перем ПоляМодальныхОкон;

&НаКлиенте
Перем СписокПропускаемыхФорм;

// { Plugin interface
&НаКлиенте
Функция ОписаниеПлагина(КонтекстЯдра, ВозможныеТипыПлагинов) Экспорт
	Возврат ОписаниеПлагинаНаСервере(ВозможныеТипыПлагинов);
КонецФункции

&НаСервере
Функция ОписаниеПлагинаНаСервере(ВозможныеТипыПлагинов)
	КонтекстЯдраНаСервере = ВнешниеОбработки.Создать("xddTestRunner");
	Возврат ЭтотОбъектНаСервере().ОписаниеПлагина(КонтекстЯдраНаСервере, ВозможныеТипыПлагинов);
КонецФункции

&НаКлиенте
Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	УстановитьНастройкиМодальныхОконПоУмолчанию();
КонецПроцедуры

&НаКлиенте
Функция ПортПоУмолчанию() Экспорт
	Если Не ЗначениеЗаполнено(ПортПоУмолчанию) Тогда
		УстановитьПортПоУмолчанию(49538);
	КонецЕсли;
	Возврат ПортПоУмолчанию;
КонецФункции

&НаКлиенте
Процедура УстановитьПортПоУмолчанию(Знач Порт) Экспорт
	ПортПоУмолчанию = Порт;
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьТестКлиент_ПакетныйРежим(Параметры_xddTestClient, ДопПараметры = Неопределено) Экспорт

	Если ЗначениеЗаполнено(Параметры_xddTestClient) И ТипЗнч(Параметры_xddTestClient[0]) <> Тип("ФиксированныйМассив")
				Тогда
		НовыйМассивПараметров = Новый Массив;
		НовыйМассивПараметров.Добавить(Параметры_xddTestClient);
		Параметры_xddTestClient = НовыйМассивПараметров;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ДопПараметры) Тогда
		ДопПараметры = "";
	Иначе
		ДопПараметры = ДопПараметры[0];
	КонецЕсли;

	Для Каждого ОчередныеПараметры Из Параметры_xddTestClient Цикл
		Попытка
			ПользовательПарольПорт = РазложитьСтрокуВМассивПодстрок(ОчередныеПараметры[0], ":");
			Если ПользовательПарольПорт.Количество() = 3 Тогда
				ОписаниеТестКлиента = ПодключитьТестКлиент(
					ПользовательПарольПорт[0],
					ПользовательПарольПорт[1],
					ПользовательПарольПорт[2],
					ДопПараметры);
				ТестКлиент = ОписаниеТестКлиента.Клиент;

				ЗапомнитьДанныеТестКлиента(ТестКлиент, ПользовательПарольПорт[0], ОписаниеТестКлиента.Порт,
					ДопПараметры);
			Иначе
				ОписаниеТестКлиента = ПодключитьТестКлиент("", "", 0, ДопПараметры);
				ТестКлиент = ОписаниеТестКлиента.Клиент;

				ЗапомнитьДанныеТестКлиента(ТестКлиент, "", ОписаниеТестКлиента.Порт, ДопПараметры);
			КонецЕсли;
		Исключение
			Инфо = ИнформацияОбОшибке();
			ОписаниеОшибки = "Ошибка подключения тест-клиента в пакетном режиме.
			|" + ПодробноеПредставлениеОшибки(Инфо);

			ЗаписатьОшибкуВЖурналеРегистрации(ОписаниеОшибки);

			Сообщить(ОписаниеОшибки, СтатусСообщения.ОченьВажное);
		КонецПопытки;
	КонецЦикла;

КонецПроцедуры


// Подключить тест-клиент
//
// Параметры:
//  ИмяПользователя	 - Строка	 - имя\логин пользователя
//  Пароль			 - Строка	 - пароль пользователя
//  Порт			 - Число	 - порт подключения. Порт может быть изменен.
//  ДопПараметры	 - Произвольный	 - доп.параметры, которые допустимы на клиенте
//
// Возвращаемое значение:
//   Структура - описание тест-клиента, если удалось подключить, или Неопределено
//   	* Клиент - ТестируемоеПриложение, Неопределено - тестовый клиент, если удалось подключиться
// 		* Порт - Число - порт подключения. Совпадет с переданным, если порт был не занят, и выдается новый, если исходный порт был занят
//
&НаКлиенте
Функция ПодключитьТестКлиент(Знач ИмяПользователя = "", Знач Пароль = "", Знач Порт = 0, Знач ДопПараметры = "") Экспорт

	Перем ПодключенныйТестКлиент;

	Результат = НовоеОписаниеТестКлиента();

	Порт = ПолучитьПорт(Порт);
	Порт = НайтиСвободныйПортЕслиТекущийЗанят(Порт);

	Если Не ЗначениеЗаполнено(СписокПропускаемыхФорм) Тогда
		СписокПропускаемыхФорм = Новый СписокЗначений;
	КонецЕсли;

	ПодключенныйТестКлиент = Неопределено;

	Попытка
		Выполнить "ПодключенныйТестКлиент = Новый ТестируемоеПриложение(, XMLСтрока(Порт));";
	Исключение
		Инфо = ИнформацияОбОшибке();
		ОписаниеОшибки = "Ошибка подключения тест-клиента по порту " + Порт + "
		|" + ПодробноеПредставлениеОшибки(Инфо);

		ЗаписатьПредупреждениеВЖурналРегистрации(ОписаниеОшибки);

		ПодключенныйТестКлиент = Неопределено;
	КонецПопытки;

	Если ПодключенныйТестКлиент = Неопределено Тогда
		ВызватьИсключение "Не удалось создать объект ТестируемоеПриложение.
		|Возможно, что 1С:Предприятие 8 не было запущено в режиме Менеджера тестирования (ключ командной строки /TESTMANAGER)
		|При запуске Предприятия через Конфигуратор можно включить этот режим в параметрах конфигуратора Сервис -> Параметры -> Запуск 1С:Предприятия -> Дополнительные -> Автоматизированное тестирование -> пункт ""Запускать как менеджер тестирования"".";
	КонецЕсли;

	// Попытка подключиться к уже запущенному приложению.
	Подключен = Ложь;
	Попытка
		ПодключенныйТестКлиент.УстановитьСоединение();
		Подключен = Истина;
	Исключение
		Инфо = ИнформацияОбОшибке();
		ОписаниеОшибки = "Ошибка соединения с тест-клиентом. Порт " + Порт + "
		|" + ПодробноеПредставлениеОшибки(Инфо);

		ЗаписатьПредупреждениеВЖурналРегистрации(ОписаниеОшибки);

		Подключен = Ложь;
	КонецПопытки;

	Если Подключен Тогда
		Результат.Вставить("Клиент", ПодключенныйТестКлиент);
		Результат.Вставить("Порт", Порт);

		Возврат Результат;
	КонецЕсли;

	СтрокаЗапуска = СтрокаЗапускаТестКлиента(ИмяПользователя, Пароль, Порт, ДопПараметры);

	УправлениеПриложениями = КонтекстЯдра.Плагин("УправлениеПриложениями");
	УправлениеПриложениями.ВыполнитьКомандуОСБезПоказаЧерногоОкна(СтрокаЗапуска, Ложь, Ложь);

	ВремяОкончанияОжидания = ТекущаяДата() + ТаймаутВСекундах();
	ОписаниеОшибкиСоединения = "";
	СчетчикПопыток = 0;
	Пока Не ТекущаяДата() >= ВремяОкончанияОжидания Цикл
		Попытка
			СчетчикПопыток = СчетчикПопыток + 1;

			ПодключенныйТестКлиент.УстановитьСоединение();
			Подключен = Истина;

			ЗаписатьИнформациюВЖурналРегистрации(СтрШаблон("Тест-клиент подключен успешно. Порт %1", Порт));
			Прервать;
		Исключение
			Инфо = ИнформацияОбОшибке();
			ОписаниеОшибки = СтрШаблон("Попытка %1. Ошибка подключения тест-клиента. Порт %2
			|%3", СчетчикПопыток, Порт, ПодробноеПредставлениеОшибки(Инфо));

			ЗаписатьПредупреждениеВЖурналРегистрации(ОписаниеОшибки);

		КонецПопытки;
	КонецЦикла;

	Если Не Подключен Тогда
		Попытка
			ПодключенныйТестКлиент.УстановитьСоединение();
			ЗаписатьИнформациюВЖурналРегистрации(СтрШаблон("Тест-клиент подключен успешно. Порт %1", Порт));
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ОписаниеОшибкиСоединения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
			ПолныйТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);

			ЗаписатьПредупреждениеВЖурналРегистрации(ПолныйТекстСообщения);

			ВызватьИсключение СтрШаблон(
			"Не смогли установить соединение с тестовым приложением для пользователя %1!
			|%2",
			ИмяПользователя, ОписаниеОшибкиСоединения);
		КонецПопытки;
	КонецЕсли;

	Если Подключен И ОсновноеОкно(ПодключенныйТестКлиент) = Неопределено Тогда
		Таймаут = 5;
		ДлительностьОжидания = 0;
		Попытка
			Пока ОсновноеОкно(ПодключенныйТестКлиент) = Неопределено И ДлительностьОжидания < ТаймаутВСекундах() Цикл
				ПодключенныйТестКлиент.ПолучитьАктивноеОкно().Активизировать();
				ДлительностьОжидания = ДлительностьОжидания + Таймаут;

				Пауза(ПодключенныйТестКлиент, Таймаут);
			КонецЦикла;

		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ОписаниеОшибкиСоединения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
			ПолныйТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);

			ЗаписатьПредупреждениеВЖурналРегистрации(ПолныйТекстСообщения);

			ВызватьИсключение КонтекстЯдра.СтрШаблон_(
			"ru = 'Не смогли установить соединение с тестовым приложением для пользователя %1
			|%2!",
			ИмяПользователя, ОписаниеОшибкиСоединения);

		КонецПопытки;
		Если ОсновноеОкно(ПодключенныйТестКлиент) = Неопределено Тогда
			ПодключенныйТестКлиент.НайтиОбъект(Тип("ТестируемаяКнопкаФормы"), "Отмена").Нажать();
			ПодключенныйТестКлиент.РазорватьСоединение();

			ВызватьИсключение НСтр("ru = 'Превышено время ожидания ввода пароля.'");
		КонецЕсли;
	КонецЕсли;

	Результат.Вставить("Клиент", ПодключенныйТестКлиент);
	Результат.Вставить("Порт", Порт);

	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ЗавершитьВсеТестКлиенты() Экспорт

	КонтекстЯдра.Отладка("Перед завершением всех тест-клиентов");

	Если Не ЗначениеЗаполнено(ЗапущенныеТестКлиенты) Тогда

		КонтекстЯдра.Отладка("	- Не найдено запущенных тест-клиентов");
		Возврат;

	КонецЕсли;

	Для Каждого ТекЗначение Из ЗапущенныеТестКлиенты Цикл

		Порт = XMLСтрока(ТекЗначение.Порт);
		КонтекстЯдра.Отладка("	- Нашли тест-клиент. Порт " + Порт);

		Если ЭтоLinux() Тогда
			ЗапуститьПриложение("kill -9 `ps aux | grep -ie TESTCLIENT | grep -ie 1cv8c | awk '{print $2}'`");
		Иначе
			ТекстСкрипта = ТекстСкриптаЗавершитьТестКлиент(Порт);
			КонтекстЯдра.Отладка("	- текст скрипта удаления. " + ТекстСкрипта);

			КодВозврата = Неопределено;
			ЗапуститьПриложение(ТекстСкрипта,, Истина, КодВозврата);
			Если ЗначениеЗаполнено(КодВозврата) Тогда
				КонтекстЯдра.Отладка(СтрШаблон("	- После запуска команды завершения тест-клиента. КодВозврата: %1, Порт %2", КодВозврата, Порт));
			Иначе
				КонтекстЯдра.Отладка("	- После запуска команды завершения тест-клиента. Порт " + Порт);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Функция ТестКлиентПоУмолчанию() Экспорт
	
	Если ЗначениеЗаполнено(ЗапущенныеТестКлиенты) Тогда
		Текст = СтрШаблон("ТестКлиентПоУмолчанию: Был использован один из запущенных ранее тест клиентов.
							 |	ИмяПользователя: %1
							 |	Порт: %2
							 |	ДопПараметры: %3",  ЗапущенныеТестКлиенты[0].ИмяПользователя, 
													ЗапущенныеТестКлиенты[0].Порт, 
													ЗапущенныеТестКлиенты[0].ДопПараметры);
		КонтекстЯдра.Отладка(Текст);	
		ЗаписатьПримечаниеВЖурналРегистрации(Текст);
		
		Возврат ЗапущенныеТестКлиенты[0].ТестКлиент;
	КонецЕсли;
	
	ОписаниеТестКлиента = ПодключитьТестКлиент();
	Результат = ОписаниеТестКлиента.Клиент;
	
	ЗапомнитьДанныеТестКлиента(Результат, "", ОписаниеТестКлиента.Порт, "");
	
	Текст = СтрШаблон("ТестКлиентПоУмолчанию: Был запущен новый тест клиент.
						|	Порт: %1",  ОписаниеТестКлиента.Порт);
	ЗаписатьПримечаниеВЖурналРегистрации(Текст);
	КонтекстЯдра.Отладка(Текст);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ТестКлиентПоПараметрам(Знач ИмяПользователя = "", Знач Пароль = "", Знач Порт = 0,
		Знач ДопПараметры = "") Экспорт
	Порт = ПолучитьПорт(Порт);

	Результат = НайтиЗапущенныйКлиент(ИмяПользователя, Порт);
	Если Результат <> Неопределено Тогда
		Возврат Результат;
	КонецЕсли;

	ОписаниеТестКлиента = ПодключитьТестКлиент(ИмяПользователя, Пароль, Порт, ДопПараметры);
	Результат = ОписаниеТестКлиента.Клиент;

	ЗапомнитьДанныеТестКлиента(Результат, ИмяПользователя, ОписаниеТестКлиента.Порт, ДопПараметры);

	Возврат Результат;

КонецФункции

// Процедура - Идентифицировать окно предупреждение
//
// Параметры:
//  ТестКлиент							 - ТестируемоеПриложение, ТестируемоеОкноКлиентскогоПриложения - 
//  Пояснение							 - Строка - Доп информация для вывода в содержимое ошибки
//  ИгнорируемыйМассивТекстовИсключений	 - Массив - Массив строк содержащий исключения для игнорирования
//
&НаКлиенте
Процедура ИдентифицироватьОкноПредупреждение(Знач ТестКлиент, Знач Пояснение = "", ИгнорируемыйМассивТекстовИсключений = Неопределено) Экспорт
	
	ТекущаяИнформацияОбОшибке = ТестКлиент.ПолучитьТекущуюИнформациюОбОшибке();
	Если ТипЗнч(ТекущаяИнформацияОбОшибке) = Тип("ИнформацияОбОшибке") Тогда
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ТекущаяИнформацияОбОшибке);
		КраткоеПредставление = КраткоеПредставлениеОшибки(ТекущаяИнформацияОбОшибке);
		
		Если ИгнорируемыйМассивТекстовИсключений <> Неопределено И МассивСоджержит(ИгнорируемыйМассивТекстовИсключений, КраткоеПредставление) Тогда
			ПодробноеПредставлениеОшибки = "";
			КонтекстЯдра.Отладка(СтрШаблон("ОКНА: Пропустили исключение при открытии окна. 
			|	Текст ошибки: ""%1""", КраткоеПредставление));
		Иначе 
			ЗаписатьОшибкуВЖурналеРегистрации(СтрШаблон("ИдентифицироватьОкноПредупреждение выявило ошибку:
			|""%1""", ПодробноеПредставлениеОшибки), КлючНастройкиМодальныхОкон());
		КонецЕсли;
	Иначе
		ПодробноеПредставлениеОшибки = "";
	КонецЕсли;
	
	ЗакрытьВсеОткрытыеОкнаСУчетомНастроек(ТестКлиент);
	Окна = ПредставленияОткрытыхОкон(ТестКлиент);
	Если Не ЗначениеЗаполнено(Окна) И Не ЗначениеЗаполнено(ПодробноеПредставлениеОшибки) Тогда
		Возврат;		
	КонецЕсли;
	
	ТекстИсключения = СтрШаблон_("Выявлено модальное окно:
		|[%1] 
		|Открытые окна: %2
		|Ошибка: %3",
		Пояснение,
		Окна,
		ПодробноеПредставлениеОшибки);

	КонтекстЯдра.ВызватьОшибкуПроверки(ТекстИсключения);
КонецПроцедуры

&НаКлиенте
Функция ПоявилосьОкноПредупрежденияСТекстом(Знач ТестКлиент, Знач ТекстИсключенияДляПроверки) Экспорт
	
	ОкноПредупреждение = ОкноПредупреждение(ТестКлиент);
	
	Если ТипЗнч(ОкноПредупреждение) <> Тип("ТестируемоеОкноКлиентскогоПриложения") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекстИсключения = СодержимоеОкна(ОкноПредупреждение);
	
	Возврат Найти(ВРег(ТекстИсключения), ВРег(ТекстИсключенияДляПроверки)) > 0;
	
КонецФункции

&НаКлиенте
Функция ОкноПредупреждение(ТестКлиент)

	ОкноПредупреждение = Неопределено;
	ПодчиненныеОбъекты = ПодчиненныеОбъекты(ТестКлиент);

	Для Каждого ПодчиненныйОбъект Из ПодчиненныеОбъекты Цикл
		Если ТипЗнч(ПодчиненныйОбъект) <> Тип("ТестируемоеОкноКлиентскогоПриложения") Тогда
			Продолжить;
		КонецЕсли;
		Если ПодчиненныйОбъект.Основное Или ПодчиненныйОбъект.НачальнаяСтраница Тогда
			Продолжить;
		КонецЕсли;
		
		Если НайтиОписаниеМодальногоОкна(ПодчиненныйОбъект) <> Неопределено Тогда
			ОкноПредупреждение = ПодчиненныйОбъект;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Возврат ОкноПредупреждение;

КонецФункции

&НаКлиенте
Функция НайтиОписаниеМодальногоОкна(ОкноПриложения)
	Перем Поля;
	
	// Если в настройках задан заголовок и поля, это по И определяться (так @aartbear сказал), значит
	// если на этом этапе ОписаниеМодальногоОкна = неопределено, значит нет подходящего описания для заголовка
	// возможно в описании заданы только поля, а заголовка нет, значит ищем по полям. ОписаниеМодальногоОкна <> неопределено, мы должны проверить поля именно этого описания
	// если проверять по ПоляМодальныхОкон то можно получить ситуацию когда заголовок из одной настройки, а поля из другой.
	
	// НайтиПодходящееЗначениеПоКлючуВКоллекции должен возвращать массив т.к. для обеспечения логики работы которая описано выше может быть несколько правил (описаний)
	// с одним и тем же заголовком, но разными полями и разными кнопками. Т.е. поля это способ уточнить поиск окна, заголовок первичнее
	ОписанияМодальногоОкна = НайтиПодходящееЗначениеПоКлючуВКоллекции(ЗаголовкиМодальныхОкон, ОкноПриложения.Заголовок);
	
	ПоляФормы = Новый Массив();
	ДополнитьМассив(ПоляФормы, ОкноПриложения.НайтиОбъекты(Тип("ТестируемоеПолеФормы"))); 
	ДополнитьМассив(ПоляФормы, ОкноПриложения.НайтиОбъекты(Тип("ТестируемаяДекорацияФормы"))); 
	
	Если ОписанияМодальногоОкна = Неопределено ИЛИ ОписанияМодальногоОкна.Количество() = 0 Тогда
		// отбор правил без заголовка, но с полями
		Для Каждого ТекПолеФормы Из ПоляФормы Цикл
			Настройки = НайтиПодходящееЗначениеПоКлючуВКоллекции(ПоляМодальныхОкон, ТекПолеФормы.ТекстЗаголовка);	
			Если Настройки = Неопределено Тогда
				Возврат Неопределено;	
			КонецЕсли;
			
			Для Каждого Описание Из Настройки Цикл
				// нас интересуют те описания у которыъ нет заголовка, по заголовку поиск выше, раз мы тут, значит по заголовку не нашлось
				Если Не Описание.Свойство("Заголовки") Тогда
					// кто первый тому повезло. Может быть больше если заданы одинаковые поля в разных описаниях
					Возврат Описание;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	Иначе
		// отбор правил с заголовком и с полями
		Для Каждого Описание Из ОписанияМодальногоОкна Цикл
			Если Не Описание.Свойство("Поля", Поля) Тогда
				Продолжить;	
			КонецЕсли;
			
			Для Каждого ТекстПоля Из Поля Цикл
				Для Каждого ТекПолеФормы Из ПоляФормы Цикл
					Если КонтекстЯдра.СтрокаСоответствуетШаблону(ДляСравнения(ТекПолеФормы.ТекстЗаголовка), ДляСравнения(ТекстПоля)) Тогда
						Возврат Описание;	
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		
		// отбор правил без полей, но с заголовком
		// если не нашли подходящее описание по полям, значит вернем первое подходящее без полей только с заголовком
		Для Каждого Описание Из ОписанияМодальногоОкна Цикл
			Если Не Описание.Свойство("Поля", Поля) Тогда
				Возврат Описание;
			КонецЕсли;
		КонецЦикла;

	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции


&НаКлиенте
// Возвращает основное окно текущего тест-клиента
//
// Параметры:
//   ТестКлиент - ТестируемоеПриложение - ТестируемоеПриложение
//
//  Возвращаемое значение:
//   ТестируемоеОкноКлиентскогоПриложения - или Неопределено, если не нашли или ошибка сетевого взаимодействия
//
Функция ОсновноеОкно(ТестКлиент) Экспорт
	Попытка
		КлиентскиеОкнаТестируемогоПриложения = ПодчиненныеОбъекты(ТестКлиент);
		Для Каждого ТекОкно Из КлиентскиеОкнаТестируемогоПриложения Цикл
			Если ТекОкно.Основное Тогда
				Возврат ТекОкно;
			КонецЕсли;
		КонецЦикла;
	Исключение
		Инфо = ИнформацияОбОшибке();
		ОписаниеОшибки = "Ошибка поиска основного окна тест-клиента.
		|" + ПодробноеПредставлениеОшибки(Инфо);

		ЗаписатьПредупреждениеВЖурналРегистрации(ОписаниеОшибки);
		
		Возврат Неопределено;
	КонецПопытки;

	Возврат Неопределено;
КонецФункции

&НаКлиенте
Процедура Пауза(ТестКлиент, КоличествоСекунд) Экспорт

	ТестКлиент.ОжидатьОтображениеОбъекта(Тип("ТестируемаяФорма"), "ЗаведомоОтсутствующийОбъект",, КоличествоСекунд);

КонецПроцедуры

&НаКлиенте
Функция СодержимоеОкна(ОкноПредупреждение) Экспорт

	Если ОкноПредупреждение = Неопределено Тогда
		Возврат "";	
	КонецЕсли;
	
	ТекстыЗаголовков = Новый Массив;
	Для Каждого ТекПолеФормы Из ОкноПредупреждение.НайтиОбъекты(Тип("ТестируемоеПолеФормы")) Цикл
		ТекстыЗаголовков.Добавить(ТекПолеФормы.ТекстЗаголовка);
	КонецЦикла;

	Для Каждого ТекДекорацияФормы Из ОкноПредупреждение.НайтиОбъекты(Тип("ТестируемаяДекорацияФормы")) Цикл
		Если ТекДекорацияФормы.Имя = "Message" Тогда
			ТекстыЗаголовков.Добавить(ТекДекорацияФормы.ТекстЗаголовка);
		КонецЕсли;
	КонецЦикла;

	ИсходноеСодержимое = СтрСоединить(ТекстыЗаголовков, " ");
	Содержимое = Лев(ИсходноеСодержимое, 50); // нет смысла выводить длинную портянку
	Содержимое = ?(СтрДлина(Содержимое) < СтрДлина(ИсходноеСодержимое), Содержимое+"...", Содержимое);
	
	Возврат СтрШаблон("Заголовок: <%1>; Содержимое: <%2>", ОкноПредупреждение.Заголовок, Содержимое);
КонецФункции

// Функция - Возвращает заголовок и содержимое открытых окон
//
// Параметры:
//  ТестКлиент	 - 	ТестируемоеПриложение, ТестируемоеОкноКлиентскогоПриложения - 
// 
// Возвращаемое значение:
//  Строка - Строка разделенная переносом строк, каждая строчка это представление окна
//
&НаКлиенте
Функция ПредставленияОткрытыхОкон(ТестКлиент) Экспорт
	ОткрытыеОкна = ПодчиненныеОбъекты(ТестКлиент);
	ОписанияОкон = Новый Массив();
	Для а = 0 По ОткрытыеОкна.ВГраница() Цикл
		Если ОткрытыеОкна[а].Основное ИЛИ ОткрытыеОкна[а].НачальнаяСтраница
			ИЛИ ТипЗнч(ОткрытыеОкна[а]) <> Тип("ТестируемоеОкноКлиентскогоПриложения") Тогда
			Продолжить;
		КонецЕсли;

		ОписанияОкон.Добавить(СтрШаблон("[%1] %2", а, СодержимоеОкна(ОткрытыеОкна[а])));
	КонецЦикла;
	
	Возврат СтрСоединить(ОписанияОкон, Символы.ПС);
КонецФункции


&НаКлиенте
Процедура ЗакрытьВсеОткрытыеОкна(ТестКлиент) Экспорт   
	КонтекстЯдра.Отладка("ОКНА: Закрываем все открытые окна");

	СписокПропускаемыхФорм.Очистить();
	
	ОткрытыеОкна = ПодчиненныеОбъекты(ТестКлиент);
	Для Каждого ТекОкно Из ОткрытыеОкна Цикл
		Если ТекОкно.Основное Или ТекОкно.НачальнаяСтраница Тогда
			Продолжить;
		КонецЕсли;
		Если ПропуститьОкно(ТекОкно) Тогда
			Продолжить;
		КонецЕсли;

		Попытка
			ТекОкно.Закрыть();
			КонтекстЯдра.Отладка(СтрШаблон("	-->> Закрыли окно ""%1""", ТекОкно.Заголовок));
		Исключение
			// Необходимо принудительно закрыть все окна, специальная обработка исключений не требуется.
			СписокПропускаемыхФорм.Добавить(ТекОкно.Заголовок);
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры

// Процедура - Закрываются все открытые окна с учетом настроек
//
// Параметры:
//  ТестКлиент	 - 	ТестируемоеПриложение, ТестируемоеОкноКлиентскогоПриложения -  
//
&НаКлиенте
Процедура ЗакрытьВсеОткрытыеОкнаСУчетомНастроек(ТестКлиент) Экспорт
	КонтекстЯдра.Отладка("ОКНА: Закрываем окна с учетом настроек");

	// сначала пробуем закрыть окна, если не закрываются будем кликать по кнопкам с учетом настроек
	ЗакрытьВсеОткрытыеОкна(ТестКлиент);
	
	СписокПропускаемыхФорм.Очистить();
	ОткрытыеОкна = ПодчиненныеОбъекты(ТестКлиент);
	Для Каждого ТекОкно Из ОткрытыеОкна Цикл
		Если ТекОкно.Основное ИЛИ ТекОкно.НачальнаяСтраница ИЛИ ПропуститьОкно(ТекОкно)
			ИЛИ ТипЗнч(ТекОкно) <> Тип("ТестируемоеОкноКлиентскогоПриложения") Тогда
			Продолжить;
		КонецЕсли;
		
		Кнопки = ПолучитьКнопки(ТекОкно);
		Если Не ЗначениеЗаполнено(Кнопки) Тогда
			Продолжить;
		КонецЕсли;
				
		ОкноПредупреждение = ОкноПредупреждение(ТестКлиент);
		ОкноЗакрыто = Ложь;
		Если ОкноПредупреждение = Неопределено Тогда
			ОкноЗакрыто = НажатьКнопкуМодальногоДиалогаЕслиНаФормеВсегоОднаКнопка(Кнопки, ТекОкно.Заголовок);
		Иначе
			ОкноЗакрыто = ЗакрытьОкноПредупреждения(Кнопки, ОкноПредупреждение);
		КонецЕсли;
		
		Если ОкноЗакрыто Тогда
			КонтекстЯдра.Отладка(СтрШаблон("	-->> Закрыли окно ""%1""", ТекОкно.Заголовок));
			
			// успешное нажатие на кнопку может повторно открыть модальное окно, например при закрытии формы документа появляется вопрос
			// сохранить? жмется да, появляется еще окно "произвести перерасчет?"
			// или когда при открытии окна появляется модальное, мы его закрываем по правилам, а как закрыть основное окно, вот для этого и подходит данная рекурсия 
			ЗакрытьВсеОткрытыеОкнаСУчетомНастроек(ТестКлиент); 
		Иначе
			КонтекстЯдра.Отладка(СтрШаблон("	-->> Настроек для окна ""%1"" не найдено", ТекОкно.Заголовок));	
		КонецЕсли;
		
		СписокПропускаемыхФорм.Добавить(ТекОкно.Заголовок);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция ПолучитьКнопки(Окно)
	Кнопки = Новый Массив();
	Попытка
		Для Каждого Кнопуля Из Окно.НайтиОбъекты(Тип("ТестируемаяКнопкаФормы")) Цикл
			Если Кнопуля.Вид = ВидКнопкиФормы.ОбычнаяКнопка И Не СтрНачинаетсяС_(Кнопуля.ТекстЗаголовка, "Command") Тогда
				Кнопки.Добавить(Кнопуля);
			КонецЕсли;
		КонецЦикла;
	Исключение
		Возврат Кнопки;
	КонецПопытки;
	
	Возврат Кнопки;
КонецФункции

&НаКлиенте
Функция ПропуститьОкно(ТекОкно)

	Если ТекОкно.Основное Или ТекОкно.НачальнаяСтраница Тогда
		Возврат Истина;
	КонецЕсли;

	// В массив добавляем окна, на которых уже вылезали ошибки
	Если СписокПропускаемыхФорм.НайтиПоЗначению(ТекОкно.Заголовок) <> Неопределено Тогда
		Возврат Истина;
	КонецЕсли;

	Возврат Ложь;
КонецФункции

&НаКлиенте
Функция ЗакрытьОкноПредупреждения(Кнопки, ОкноПриложения)
	Результат = Ложь;
	ОписаниеМодальногоОкна = НайтиОписаниеМодальногоОкна(ОкноПриложения);
	
	ОкноПриложения.Активизировать();
	Если ОписаниеМодальногоОкна <> Неопределено И НажатьКнопкуМодальногоДиалога(ОписаниеМодальногоОкна, Кнопки) Тогда
		КонтекстЯдра.Отладка(СтрШаблон("ОКНА: Успешный клик по кнопке окна <%1>", ОкноПриложения.Заголовок));
		Результат = Истина;
	Иначе
		КонтекстЯдра.Отладка(СтрШаблон("ОКНА: Не удалось нажать на кнопку окна <%1>", ОкноПриложения.Заголовок));
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция ОткрытыеОкна(ТестКлиент) Экспорт

	ОткрытыеОкна = Новый Соответствие;
	ВсеОкна = ТестКлиент.НайтиОбъекты(Тип("ТестируемоеОкноКлиентскогоПриложения"));
	Для Каждого ТекОкно Из ВсеОкна Цикл
		Если ТекОкно = Неопределено Или ТекОкно.Основное Или ТекОкно.НачальнаяСтраница Тогда
			Продолжить;
		КонецЕсли;
		ОткрытыеОкна.Вставить(ТекОкно.Заголовок, ТекОкно);
	КонецЦикла;

	Возврат ОткрытыеОкна;
КонецФункции

&НаКлиенте
Процедура ПроверитьНаНовыеМодальныеОкна(ТестКлиент, ОткрытыеОкнаДо) Экспорт
	НужноВыброситьИсключение = Ложь;
	Попытка
		ОткрытыеОкна = ТестКлиент.НайтиОбъекты(Тип("ТестируемоеОкноКлиентскогоПриложения"));
		Для Каждого ТекОкно Из ОткрытыеОкна Цикл
			Если ТекОкно = Неопределено Или ТекОкно.Основное Или ТекОкно.НачальнаяСтраница
					Или ОткрытыеОкнаДо.Получить(ТекОкно.Заголовок) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
	
			НужноВыброситьИсключение = Истина;
			Прервать;
		КонецЦикла;
	Исключение
		Инфо = ИнформацияОбОшибке();
		ОписаниеОшибки = "Ошибка поиска основного окна тест-клиента.
		|" + ПодробноеПредставлениеОшибки(Инфо);

		ЗаписатьПредупреждениеВЖурналРегистрации(ОписаниеОшибки);
		
		КонтекстЯдра.ВывестиСообщение("Не удалось проверить модальные окна ",
			СтатусСообщения.ОченьВажное);
		
		Возврат;
	КонецПопытки;

	Если НужноВыброситьИсключение Тогда
		ТекстИсключения = КонтекстЯдра.СтрШаблон_("
		|Выявлено окно, которое не закрывается!
		|Возможно, это модальное окно.
		|Окно: %1", СодержимоеОкна(ТекОкно));
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьНастройкиМодальныхОкон(ПарамНастройкиМодальныхОкон) Экспорт
	Если Не ЗначениеЗаполнено(ПарамНастройкиМодальныхОкон) Тогда
		Возврат;
	КонецЕсли;

	Если НастройкиМодальныхОкон <> ПарамНастройкиМодальныхОкон Тогда
		Для Каждого КлючЗначение Из ПарамНастройкиМодальныхОкон Цикл
			НастройкиМодальныхОкон.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
		КонецЦикла;
	КонецЕсли;

	ЗаголовкиМодальныхОкон = Новый Соответствие;
	Для Каждого КлючЗначение Из НастройкиМодальныхОкон Цикл
		Описание = КлючЗначение.Значение;
		Если ТипЗнч(Описание) <> Тип("Структура") Тогда
			Продолжить;
		КонецЕсли;
		
		Заголовки = Неопределено;
		Если Описание.Свойство("Заголовки", Заголовки) Тогда
			Для Каждого Заголовок Из Заголовки Цикл
				Если ЗаголовкиМодальныхОкон[ДляСравнения(Заголовок)] = Неопределено Тогда
					ЗаголовкиМодальныхОкон[ДляСравнения(Заголовок)] = Новый Массив();
				КонецЕсли;

				ЗаголовкиМодальныхОкон[ДляСравнения(Заголовок)].Добавить(Описание);
			КонецЦикла;
		КонецЕсли;
		
		Поля = Неопределено;
		Если Описание.Свойство("Поля", Поля) Тогда
			Для Каждого ТекстПоля Из Поля Цикл
				Если ПоляМодальныхОкон[ДляСравнения(ТекстПоля)] = Неопределено Тогда
					ПоляМодальныхОкон[ДляСравнения(ТекстПоля)] = Новый Массив();
				КонецЕсли;
				
				ПоляМодальныхОкон[ДляСравнения(ТекстПоля)].Добавить(Описание);
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция ПолучитьНастройкиМодальныхОкон() Экспорт
	Возврат НастройкиМодальныхОкон;
КонецФункции

&НаКлиенте
Функция КлючНастройкиМодальныхОкон() Экспорт
	Возврат "МодальныеОкна";
КонецФункции

// Найти свободный порт, если текущий\указанный порт занят другим процессом
//
// Параметры:
//  Порт					 - Число	 - Необязательный. По умолчанию 1538
//  НачалоДиапазонаПортов	 - Число	 - Необязательный. По умолчанию 48000
//  ОкончаниеДиапазонаПортов - Число	 - Необязательный. По умолчанию 50000
//
// Возвращаемое значение:
//   Число - текущий порт или новый порт, если текущий занят
//
&НаКлиенте
Функция НайтиСвободныйПортЕслиТекущийЗанят(Знач Порт = 1538, Знач НачалоДиапазонаПортов = 48000, Знач ОкончаниеДиапазонаПортов = 50000) Экспорт

	Если Порт <= 0 Или Порт > 65536 Тогда
		Порт = 1538;
	КонецЕсли;

	СведенияОЗанятыхПортах = СведенияОЗанятыхПортах();
	МассивЗанятыхПортов = СведенияОЗанятыхПортах.Порты;
	ПроцессыДляПортов = СведенияОЗанятыхПортах.Процессы;

	Если МассивЗанятыхПортов.Найти(Порт) <> Неопределено Тогда
		ЗаписатьПредупреждениеВЖурналРегистрации(СтрШаблон("Порт ""%1"" занят процессом pid: ""%2"" ", Порт, ПроцессыДляПортов[Порт]));

		Для СвободныйПорт = НачалоДиапазонаПортов По ОкончаниеДиапазонаПортов Цикл
			Если МассивЗанятыхПортов.Найти(СвободныйПорт) = Неопределено Тогда

				ЗаписатьИнформациюВЖурналРегистрации(СтрШаблон("Будет использован свободный порт: ""%1""", СвободныйПорт));
				Возврат СвободныйПорт;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Возврат Порт;

КонецФункции

// } Plugin interface

// { Helpers
&НаСервере
Функция ЭтотОбъектНаСервере()
	Возврат РеквизитФормыВЗначение("Объект");
КонецФункции

&НаКлиенте
Функция СтрокаЗапускаТестКлиента(Знач ИмяПользователя, Знач Пароль, Знач Порт, Знач ДопПараметры)

	Если Не ЗначениеЗаполнено(ИмяПользователя) Тогда
		ИмяПользователя = ИмяТекущегоПользователя();
	КонецЕсли;

	СтрокаЗапуска1с = КаталогПрограммы() + "1cv8c";

	Если Не ЭтоLinux() Тогда
		СтрокаЗапуска1с = КонтекстЯдра.СтрШаблон_("%1.exe", СтрокаЗапуска1с);
	КонецЕсли;

	Результат = КонтекстЯдра.СтрШаблон_(
		"""%1"" ENTERPRISE /IBConnectionString""%2""%3%4 /TestClient -TPort%5 /L%6 %7 /DisableStartupMessages",
		СтрокаЗапуска1с,
		СтрЗаменить(СтрокаСоединенияИнформационнойБазы(), """", """"""),
		?(ПустаяСтрока(ИмяПользователя), "", " /N""" + ИмяПользователя + """"),
		?(ПустаяСтрока(Пароль), ""," /P""" + Пароль + """"),
		XMLСтрока(Порт),
		ТекущийЯзык(),
		ДопПараметры);

	Возврат Результат;

КонецФункции

&НаСервереБезКонтекста
Функция ИмяТекущегоПользователя()

	ТекущийПользователь = ПользователиИнформационнойБазы.ТекущийПользователь();

	Если ТекущийПользователь.АутентификацияОС Тогда
		Возврат "";
	Иначе
		Возврат ТекущийПользователь.Имя;
	КонецЕсли;

КонецФункции

&НаКлиенте
Функция ТаймаутВСекундах()

	Возврат 60;

КонецФункции

&НаКлиенте
Функция ТекстСкриптаЗавершитьТестКлиент(НомерПорта)

	Результат =
		"wmic process where (CommandLine Like ""%/TESTCLIENT%"" And ExecutablePath Like ""%1cv8c%"") call terminate";

	Если Не ЗначениеЗаполнено(НомерПорта) Тогда
		Возврат Результат;
	КонецЕсли;

	Возврат СтрЗаменить(
		Результат,
		"%/TESTCLIENT%",
		"%/TESTCLIENT -TPort" + НомерПорта + "%");

КонецФункции

&НаКлиенте
Функция ПолноеИмяИсполняемогоФайла()

	Возврат КонтекстЯдра.СтрШаблон_("%1%2%3",
	КаталогПрограммы(),
	"1cv8c",
	РасширениеИсполняемогоФайла());

КонецФункции

&НаКлиенте
Функция РасширениеИсполняемогоФайла()

	Если ЭтоLinux() Тогда
		Возврат "";
	Иначе
		Возврат ".exe";
	КонецЕсли;

КонецФункции

&НаКлиенте
Функция ЭтоLinux()

	СисИнфо = Новый СистемнаяИнформация;
	ВерсияПриложения = СисИнфо.ВерсияПриложения;

	Возврат Найти(Строка(СисИнфо.ТипПлатформы), "Linux") > 0;

КонецФункции

// Процедура - Записать примечание в журнал регистрации
//
// Параметры:
//  ПостфиксСобытия	 - Строка - Может быть пустым
//  Текст			 - 	 - 
//
&НаСервереБезКонтекста
Процедура ЗаписатьОшибкуВЖурналеРегистрации(Знач ОписаниеОшибки, ПостфиксСобытия = "")
	ИмяСобытия = СтрРазделить(ИмяСобытияЖР()+"."+ПостфиксСобытия, ".", Ложь); // эта пляска для случаев когда ПостфиксСобытия будет пустой, что б в конце не осталась точка
	ЗаписьЖурналаРегистрации(СтрСоединить(ИмяСобытия, "."), УровеньЖурналаРегистрации.Ошибка, , , ОписаниеОшибки);
КонецПроцедуры

// Процедура - Записать примечание в журнал регистрации
//
// Параметры:
//  ПостфиксСобытия	 - Строка - Может быть пустым
//  Текст			 - 	 - 
//
&НаСервереБезКонтекста
Процедура ЗаписатьПредупреждениеВЖурналРегистрации(Знач ТекстОшибки, ПостфиксСобытия = "")
	ИмяСобытия = СтрРазделить(ИмяСобытияЖР()+"."+ПостфиксСобытия, ".", Ложь);
	ЗаписьЖурналаРегистрации(СтрСоединить(ИмяСобытия, "."),
		УровеньЖурналаРегистрации.Предупреждение, , ,
		ТекстОшибки);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьИнформациюВЖурналРегистрации(Знач Текст, ПостфиксСобытия = "")
	ИмяСобытия = СтрРазделить(ИмяСобытияЖР()+"."+ПостфиксСобытия, ".", Ложь);
	ЗаписьЖурналаРегистрации(СтрСоединить(ИмяСобытия, "."), УровеньЖурналаРегистрации.Информация,,, Текст);
КонецПроцедуры

// Процедура - Записать примечание в журнал регистрации
//
// Параметры:
//  ПостфиксСобытия	 - Строка - Может быть пустым
//  Текст			 - 	 - 
//
&НаСервереБезКонтекста
Процедура ЗаписатьПримечаниеВЖурналРегистрации(Знач Текст, ПостфиксСобытия = "")
	ИмяСобытия = СтрРазделить(ИмяСобытияЖР()+"."+ПостфиксСобытия, ".", Ложь); // эта пляска для случаев когда ПостфиксСобытия будет пустой, что б в конце не осталась точка
	ЗаписьЖурналаРегистрации(СтрСоединить(ИмяСобытия, "."), УровеньЖурналаРегистрации.Примечание,,, Текст);	
КонецПроцедуры


&НаСервереБезКонтекста
Функция ИмяСобытияЖР()

	Возврат "VanessaADD.ТестКлиенты";

КонецФункции

&НаКлиенте
Процедура ЗапомнитьДанныеТестКлиента(ТестКлиент, ИмяПользователя, Порт, ДопПараметры)

	ДанныеТестКлиента = Новый Структура;
	ДанныеТестКлиента.Вставить("ТестКлиент", ТестКлиент);
	ДанныеТестКлиента.Вставить("ИмяПользователя", ИмяПользователя);
	ДанныеТестКлиента.Вставить("Порт", Порт);
	ДанныеТестКлиента.Вставить("ДопПараметры", ДопПараметры);

	Если ЗапущенныеТестКлиенты = Неопределено Тогда
		ЗапущенныеТестКлиенты = Новый Массив;
	КонецЕсли;

	ЗапущенныеТестКлиенты.Добавить(ДанныеТестКлиента);

КонецПроцедуры

&НаКлиенте
Функция НайтиЗапущенныйКлиент(ИмяПользователя, Порт)

	Если Не ЗначениеЗаполнено(ЗапущенныеТестКлиенты) Тогда
		Возврат Неопределено;
	КонецЕсли;

	Для Каждого ТекЗапущенныйКлиент Из ЗапущенныеТестКлиенты Цикл
		Если ТекЗапущенныйКлиент.ИмяПользователя = ИмяПользователя
			И ТекЗапущенныйКлиент.Порт = Порт Тогда
			Возврат ТекЗапущенныйКлиент.ТестКлиент;
		КонецЕсли;
	КонецЦикла;

КонецФункции

&НаКлиенте
Функция ПолучитьПорт(Знач Порт)
	Если Не ЗначениеЗаполнено(Порт) Тогда
		Порт = ПортПоУмолчанию();
	КонецЕсли;

	Если ТипЗнч(Порт) <> Тип("Число") Тогда
		Порт = Число(Порт);
	КонецЕсли;

	ЗаписатьИнформациюВЖурналРегистрации(СтрШаблон("Будет использован порт: ""%1""", Порт));
	Возврат Порт;
КонецФункции

&НаКлиенте
Функция НажатьКнопкуМодальногоДиалога(Знач ОписаниеМодальногоОкна, Знач Кнопки)
	Перем ИндексКнопки;
	
	Если Не ОписаниеМодальногоОкна.Свойство("Кнопка", ИндексКнопки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ИндексКнопки = Неопределено ИЛИ ИндексКнопки < 0 Тогда
		КонтекстЯдра.ВывестиСообщение("Индекс кнопки не должен быть 0 или меньше 0", СтатусСообщения.ОченьВажное);
		Возврат Ложь;
	КонецЕсли;
	
	Если ИндексКнопки >= Кнопки.Количество() Тогда
		КонтекстЯдра.ВывестиСообщение("Индекс кнопки не должен быть больше количества кнопок в форме " + Кнопки.Количество(),
			СтатусСообщения.ОченьВажное);
		Возврат Ложь;
	КонецЕсли;

	Попытка
		Кнопки[ИндексКнопки].Нажать();
		Возврат Истина;                                     
	Исключение
		Инфо = ИнформацияОбОшибке();
		ОписаниеОшибки = "Ошибка нажатия на кнопку.
		|" + ПодробноеПредставлениеОшибки(Инфо);

		ЗаписатьПредупреждениеВЖурналРегистрации(ОписаниеОшибки, КлючНастройкиМодальныхОкон());
		
		КонтекстЯдра.ВывестиСообщение("Не удалось нажать кнопку в форме", СтатусСообщения.ОченьВажное);
	КонецПопытки;

	Возврат Ложь;                                          
КонецФункции

&НаКлиенте
Функция НажатьКнопкуМодальногоДиалогаЕслиНаФормеВсегоОднаКнопка(Знач Кнопки, Заголовок)
	Если Кнопки.Количество() <> 1 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Попытка
		Кнопки[0].Нажать();
		Возврат Истина
	Исключение
		ОписаниеОшибки = "Ошибка нажатия на кнопку.
		|" + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписатьПредупреждениеВЖурналРегистрации(ОписаниеОшибки, КлючНастройкиМодальныхОкон());
		
		КонтекстЯдра.ВывестиСообщение("Не удалось нажать кнопку в форме " + Заголовок,
			СтатусСообщения.ОченьВажное);
			
		Возврат Ложь;
	КонецПопытки;
КонецФункции

&НаКлиенте
Функция ДляСравнения(Знач Строка)
	Возврат НРег(Строка);
КонецФункции

&НаКлиенте
Функция НайтиПодходящееЗначениеПоКлючуВКоллекции(Знач Соответствие, Знач ПроверяемаяСтрока)
	ПроверяемаяСтрока = ДляСравнения(ПроверяемаяСтрока);
	Значение = Соответствие.Получить(ПроверяемаяСтрока);
	Если Значение <> Неопределено Тогда
		Возврат Значение;
	КонецЕсли;

	Для каждого КлючЗначение Из Соответствие Цикл
		СтрокаИлиШаблон = ДляСравнения(КлючЗначение.Ключ);
		Если КонтекстЯдра.СтрокаСоответствуетШаблону(ПроверяемаяСтрока, СтрокаИлиШаблон) Тогда
			Возврат КлючЗначение.Значение;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
КонецФункции

&НаКлиенте
Функция РазложитьСтрокуВМассивПодстрок(Знач Строка, Знач Разделитель = ",",
										Знач ПропускатьПустыеСтроки = Неопределено) Экспорт

	Результат = Новый Массив;

	// для обеспечения обратной совместимости
	Если ПропускатьПустыеСтроки = Неопределено Тогда
		ПропускатьПустыеСтроки = ?(Разделитель = " ", Истина, Ложь);
		Если ПустаяСтрока(Строка) Тогда
			Если Разделитель = " " Тогда
				Результат.Добавить("");
			КонецЕсли;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;

	Позиция = Найти(Строка, Разделитель);
	Пока Позиция > 0 Цикл
		Подстрока = Лев(Строка, Позиция - 1);
		Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Подстрока) Тогда
			Результат.Добавить(Подстрока);
		КонецЕсли;
		Строка = Сред(Строка, Позиция + СтрДлина(Разделитель));
		Позиция = Найти(Строка, Разделитель);
	КонецЦикла;

	Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Строка) Тогда
		Результат.Добавить(Строка);
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Замена функции СтрШаблон на конфигурациях с режимом совместимости < 8.3.6
// При внедрении в конфигурацию с режимом совместимости >= 8.3.6 данную функцию необходимо удалить
//
&НаКлиенте
Функция СтрШаблон_(Знач СтрокаШаблон, Знач Парам1 = Неопределено, Знач Парам2 = Неопределено,
	Знач Парам3 = Неопределено, Знач Парам4 = Неопределено, Знач Парам5 = Неопределено) Экспорт

	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Парам1);
	МассивПараметров.Добавить(Парам2);
	МассивПараметров.Добавить(Парам3);
	МассивПараметров.Добавить(Парам4);
	МассивПараметров.Добавить(Парам5);

	Для Сч = 1 По МассивПараметров.Количество() Цикл
		ТекЗначение = МассивПараметров[Сч-1];
		СтрокаШаблон = СтрЗаменить(СтрокаШаблон, "%"+Сч, Строка(ТекЗначение));
	КонецЦикла;
	Возврат СтрокаШаблон;
КонецФункции

&НаКлиенте
Функция СтрНачинаетсяС_(Строка, СтрокаПоиска) Экспорт
	Возврат Найти(Строка, СтрокаПоиска) = 1;
КонецФункции

///  Объединяет строки из массива в строку с разделителями.
//
// Параметры:
//  Массив      - Массив - массив строк которые необходимо объединить в одну строку;
//  Разделитель - Строка - любой набор символов, который будет использован в качестве разделителя.
//
// Возвращаемое значение:
//  Строка - строка с разделителями.
//
&НаКлиенте
Функция СтрСоединить_(Массив, Разделитель = ",") Экспорт

	Результат = "";

	Для Индекс = 0 По Массив.ВГраница() Цикл
		Подстрока = Массив[Индекс];

		Если ТипЗнч(Подстрока) <> Тип("Строка") Тогда
			Подстрока = Строка(Подстрока);
		КонецЕсли;

		Если Индекс > 0 Тогда
			Результат = Результат + Разделитель;
		КонецЕсли;

		Результат = Результат + Подстрока;
	КонецЦикла;

	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура УстановитьНастройкиМодальныхОконПоУмолчанию()
	НастройкиМодальныхОкон = Новый Структура;
	ЗаголовкиМодальныхОкон = Новый Соответствие;
	ПоляМодальныхОкон = Новый Соответствие;

    Описание = Новый Структура;
    НастройкиМодальныхОкон.Вставить("Предприятие", Описание);
    Заголовки = Новый Массив;
    Описание.Вставить("Заголовки", Заголовки);
    Заголовки.Добавить("1С:Предприятие");
    Заголовки.Добавить("1C:Enterprise");
    Описание.Вставить("Кнопка", 0);

    Описание = Новый Структура;
    НастройкиМодальныхОкон.Вставить("ПредупреждениеБезопасности", Описание);
    Заголовки = Новый Массив;
    Описание.Вставить("Заголовки", Заголовки);
    Заголовки.Добавить("Предупреждение безопасности");
    Описание.Вставить("Кнопка", 0);

    Описание = Новый Структура;
    НастройкиМодальныхОкон.Вставить("ИзменениеДанных", Описание);
    Поля = Новый Массив;
    Описание.Вставить("Поля", Поля);
    Поля.Добавить("данные были изменены");
    Поля.Добавить("сохранить данные");
    Описание.Вставить("Кнопка", 1);

    Описание = Новый Структура;
    НастройкиМодальныхОкон.Вставить("ДанныеБылиИзменены", Описание);
    Заголовки = Новый Массив;
    Описание.Вставить("Заголовки", Заголовки);
    Заголовки.Добавить("Данные были изменены");
    Описание.Вставить("Кнопка", 1);

    Описание = Новый Структура;
    НастройкиМодальныхОкон.Вставить("ВыборТипаДанных", Описание);
    Заголовки = Новый Массив;
    Описание.Вставить("Заголовки", Заголовки);
    Заголовки.Добавить("Выбор типа данных");
    Описание.Вставить("Кнопка", 0);

    Описание = Новый Структура;
    НастройкиМодальныхОкон.Вставить("СписокЗначений", Описание);
    Заголовки = Новый Массив;
    Описание.Вставить("Заголовки", Заголовки);
    Заголовки.Добавить("Список значений");
    Описание.Вставить("Кнопка", 0);

	УстановитьНастройкиМодальныхОкон(НастройкиМодальныхОкон);

КонецПроцедуры


&НаКлиенте
Функция СведенияОЗанятыхПортах()

	Результат = Новый Структура;
	Результат.Вставить("Порты", Новый Массив);
	Результат.Вставить("Процессы", Новый Соответствие);

	// файл в методе не будет удаляться, т.к. в асинхронном режиме это довольно неудобно. сервер 1С удалит сам.
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("txt"); // BSLLS:MissingTemporaryFileDeletion-off

	Если НЕ ЭтоLinux() Тогда

		СтрокаЗапуска = "netstat -ano > """ + ИмяВременногоФайла + """ 2>&1";
		Текст = ОткрытьФайл(ИмяВременногоФайла);
		Если Текст = Неопределено Тогда
			Возврат Результат;
		КонецЕсли;

		//TODO проверить быстрое использование ВыполнитьКомандуОСБезПоказаЧерногоОкна(..., Истина, Ложь);
		//ВыполнитьКомандуОСБезПоказаЧерногоОкна(ТекстКоманды, Истина, Истина);
		УправлениеПриложениями = КонтекстЯдра.Плагин("УправлениеПриложениями");
		КодОтвета = УправлениеПриложениями.ВыполнитьКомандуОСБезПоказаЧерногоОкна(СтрокаЗапуска);
		Если КодОтвета <> 0 Тогда
			Ошибка = Текст.Прочитать();
			Текст.Закрыть();
			
			КонтекстЯдра.ВывестиСообщениеВЛогФайл(СтрШаблон("%1ОШИБКА: Не удалось выполнить команду ""%2"", код ответа = %3
														|Ошибка: ""%4""", Символы.ПС, СтрокаЗапуска, КодОтвета, Ошибка));
			
			ЗаписатьОшибкуВЖурналеРегистрации(СтрШаблон("Не удалось выполнить команду ""%1"", код ответа = %2", СтрокаЗапуска, КодОтвета));
			Возврат Результат;
		КонецЕсли;

		Пока Истина Цикл
			Стр = Текст.ПрочитатьСтроку();
			Если Стр = Неопределено Тогда
				Прервать;
			КонецЕсли;

			Если Стр = "" Тогда
				Продолжить;
			КонецЕсли;

			Разбивка = СтрРазделить(Стр, " ", Ложь);
			Если Разбивка.Количество() <> 5 Тогда
				Продолжить;
			КонецЕсли;
			Порт_ = Разбивка[1];
			PID = Разбивка[Разбивка.ВГраница()];

			Порт = "";
			Для НомерСимвола = 0 По СтрДлина(Порт_) - 1 Цикл
				СимволСтроки = Сред(Порт_, СтрДлина(Порт_) - НомерСимвола, 1);
				Если СимволСтроки = ":" Тогда
					Прервать;
				КонецЕсли;
				Если Найти("0123456789", СимволСтроки) > 0 Тогда
					Порт = СимволСтроки + Порт;
				КонецЕсли;
			КонецЦикла;

			Попытка
				Порт = Число(Порт);
				Результат.Порты.Добавить(Порт);
				Результат.Процессы.Вставить(Порт, PID);
			Исключение
				Продолжить;
			КонецПопытки;
		КонецЦикла;

		Текст.Закрыть();

	Иначе

		СтрокаЗапуска = "ss -tuwan4p 2>/dev/null | awk '{print substr($5, index($5, "":"")+1) "" "" substr($7, index($7, ""pid="")+4)}' | grep ""[\d]+\s[\d]*"" -Po | sort | uniq  > """ + ИмяВременногоФайла + """";
		//ВыполнитьКомандуОС(СтрокаЗапуска);
		УправлениеПриложениями = КонтекстЯдра.Плагин("УправлениеПриложениями");
		УправлениеПриложениями.ВыполнитьКомандуОСБезПоказаЧерногоОкна(СтрокаЗапуска);

		Текст = ОткрытьФайл(ИмяВременногоФайла);
		Если Текст = Неопределено Тогда
			Возврат Результат;
		КонецЕсли;

		Пока Истина Цикл
			Стр = Текст.ПрочитатьСтроку();
			Если Не ЗначениеЗаполнено(Стр) Тогда
				Прервать;
			КонецЕсли;

			Разбивка = СтрРазделить(Стр, " ", Ложь);
			Если Разбивка.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;

			Порт = СокрЛП(Разбивка[0]);
			Попытка
				Порт = Число(Порт);
				Результат.Порты.Добавить(Порт);

				Если Разбивка.Количество() = 2 Тогда
					Результат.Процессы.Вставить(Порт, СокрЛП(Разбивка[1]));
				КонецЕсли;
			Исключение
				Продолжить;
			КонецПопытки;
		КонецЦикла;

		Текст.Закрыть();
	КонецЕсли;

	//Для каждого СтрокаДанныеКлиентовТестирования Из ДанныеКлиентовТестирования Цикл // TODO
	//	Результат.Добавить(СтрокаДанныеКлиентовТестирования.ПортЗапускаТестКлиента);
	//КонецЦикла;

	Возврат Результат;

КонецФункции

&НаКлиенте
Функция ОткрытьФайл(Знач ПутьФайла)

	Текст = Новый ЧтениеТекста;
	Попытка

		Текст.Открыть(ПутьФайла, "UTF-8");

	Исключение

		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ПолныйТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);

		ПолныйТекстОшибки = СтрШаблон("Не удалось прочитать файл-результат команды получения занятых портов
		|%1", ПолныйТекстОшибки);

		ЗаписатьОшибкуВЖурналеРегистрации(ПолныйТекстОшибки);

		Возврат Неопределено;

	КонецПопытки;

	Возврат Текст;

КонецФункции

&НаКлиенте
Функция НовоеОписаниеТестКлиента()

	Результат = Новый Структура;
	Результат.Вставить("Клиент", Неопределено);
	Результат.Вставить("Порт", 0);

	Возврат Результат;

КонецФункции

// Дополняет массив (взята типовая процедура)
//
// Параметры:
//  МассивПриемник - Массив - массив, в который необходимо добавить значения.
//  МассивИсточник - Массив - массив значений для заполнения.
//  ТолькоУникальныеЗначения - Булево - если истина, то в массив будут включены только уникальные значения.
//
&НаКлиентеНаСервереБезКонтекста
Процедура ДополнитьМассив(МассивПриемник, Знач МассивИсточник, ТолькоУникальныеЗначения = Ложь) Экспорт
	
	Если ТолькоУникальныеЗначения Тогда
		
		УникальныеЗначения = Новый Соответствие;          
		
		Для Каждого Значение Из МассивПриемник Цикл
			УникальныеЗначения.Вставить(Значение, Истина);
		КонецЦикла;
		
		Для Каждого Значение Из МассивИсточник Цикл
			Если УникальныеЗначения[Значение] = Неопределено Тогда
				МассивПриемник.Добавить(Значение);
				УникальныеЗначения.Вставить(Значение, Истина);
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		
		Для Каждого Значение Из МассивИсточник Цикл
			МассивПриемник.Добавить(Значение);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиентеНаСервереБезКонтекста
Функция ЗначениеВМассиве(Значение)
	Массив = Новый Массив;
	Массив.Добавить(Значение);
	
	Возврат Массив;                 		
КонецФункции 


// Функция - функция обертка над ПолучитьПодчиненныеОбъекты. Попытка обойти ошибку "Ошибка сетевого взаимодействия при вызове"
//
// Параметры:
//  ТестКлиент	 - 	ТестируемоеПриложение, ТестируемоеОкноКлиентскогоПриложения - 
// 
// Возвращаемое значение:
//  ФиксированныйМассив -  Получает коллекцию объектов подчиненных текущему.
//
&НаКлиенте
Функция ПодчиненныеОбъекты(ТестКлиент) 
	Если ТестКлиент = Неопределено Тогда
		Возврат Неопределено;	
	КонецЕсли;
	
	Ответ = Неопределено;
	Попытка
		Ответ = ТестКлиент.ПолучитьПодчиненныеОбъекты();
		Если Ответ <> Неопределено Тогда
			Возврат Ответ;			
		КонецЕсли;
	Исключение
		Инфо = ИнформацияОбОшибке();
		КОшибка = КраткоеПредставлениеОшибки(Инфо);
		ПОшибка = ПодробноеПредставлениеОшибки(Инфо);
		
		ЗаписатьОшибкуВЖурналеРегистрации(СтрШаблон("Не удалось получить подчиненные объекты
											|Ошибка: 
											|%1", ПОшибка));
		КонтекстЯдра.ВызватьОшибкуПроверки(СтрШаблон("Не удалось получить подчиненные объекты
													|Ошибка: 
													|%1", КОшибка));
	КонецПопытки;                                          
	
	Возврат Ответ;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СвернутьМассив(Знач Массив)
	Результат = Новый Массив;
	ДополнитьМассив(Результат, Массив, Истина);
	Возврат Результат;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция МассивСоджержит(Массив, Элемент)
	Для Каждого М Из Массив Цикл
		Если СтрНайти(Элемент, М) > 0 ИЛИ СтрНайти(М, Элемент) > 0 Тогда  // где-то может стоять точка в конце, где-то нет.
			Возврат Истина;			
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
КонецФункции


// } Helpers
