
// { Plugin interface
Функция ОписаниеПлагина(КонтекстЯдра, ВозможныеТипыПлагинов) Экспорт
	Результат = Новый Структура;
	Результат.Вставить("Тип", ВозможныеТипыПлагинов.Утилита);
	Результат.Вставить("Идентификатор", Метаданные().Имя);
	Результат.Вставить("Представление", "Построитель дерева тестов");
	
	Возврат Новый ФиксированнаяСтруктура(Результат);
КонецФункции

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	НачальнаяНастройка();
КонецПроцедуры
// } Plugin interface

Процедура НачальнаяНастройка() Экспорт
	ЭтотОбъект.ИконкиУзловДереваТестов = ИконкиУзловДереваТестов();
	ЭтотОбъект.ТипыУзловДереваТестов = ТипыУзловДереваТестов();
КонецПроцедуры

Функция ИконкиУзловДереваТестов()
	Результат = Новый Структура;
	Результат.Вставить("Папка", 0);
	Результат.Вставить("Обработка", 1);
	Результат.Вставить("Функция", 2);
	Результат.Вставить("Подсистема", 3);
	Результат.Вставить("Форма", 4);
	Результат.Вставить("ФункцияНаФорме", 5);
	Результат.Вставить("Группа", 6);
	Результат.Вставить("Отчет", 7);
	
	Возврат Новый ФиксированнаяСтруктура(Результат);
КонецФункции

Функция ТипыУзловДереваТестов()
	Результат = Новый Структура;
	Результат.Вставить("Контейнер", "Контейнер");
	Результат.Вставить("Элемент", "Элемент");
	
	Возврат Новый ФиксированнаяСтруктура(Результат);
КонецФункции

Функция СоздатьКонтейнер(ИмяКонтейнера, ИконкаУзла = Неопределено) Экспорт
	Контейнер = Новый Структура;
	Контейнер.Вставить("Ключ", Новый УникальныйИдентификатор);
	Контейнер.Вставить("Тип", ТипыУзловДереваТестов.Контейнер);
	Контейнер.Вставить("Имя", ИмяКонтейнера);
	Контейнер.Вставить("Путь", "");
	Контейнер.Вставить("Строки", Новый Массив);
	Контейнер.Вставить("ИконкаУзла", ?(ИконкаУзла = Неопределено, ИконкиУзловДереваТестов.Папка, ИконкаУзла));
	Контейнер.Вставить("СлучайныйПорядокВыполнения", Истина);
	Контейнер.Вставить("ПродолжитьВыполнениеПослеПаденияТеста", Истина);
	Контейнер.Вставить("Контекст", Неопределено);
	Контейнер.Вставить("ЭлементДеструктор", Неопределено);
	
	Возврат Контейнер;
КонецФункции

Функция СоздатьЭлемент(Путь, ИмяМетода, Представление = "", ИконкаУзла = Неопределено) Экспорт
	Элемент = Новый Структура;
	Элемент.Вставить("Ключ", Новый УникальныйИдентификатор);
	Элемент.Вставить("Тип", ТипыУзловДереваТестов.Элемент);
	Элемент.Вставить("Путь", Путь);
	Элемент.Вставить("ИмяМетода", ИмяМетода);
	Элемент.Вставить("Представление", ?(ПустаяСтрока(Представление), ИмяМетода, Представление));
	Элемент.Вставить("ИконкаУзла", ?(ИконкаУзла = Неопределено, ИконкиУзловДереваТестов.Функция, ИконкаУзла));
	Элемент.Вставить("Параметры", Новый Массив);
	Элемент.Вставить("ПередЗапускомТеста", "ПередЗапускомТеста");
	Элемент.Вставить("ПослеЗапускаТеста", "ПослеЗапускаТеста");
	Элемент.Вставить("ПропуститьТест", Ложь);
	
	Возврат Элемент;
КонецФункции

НачальнаяНастройка();
