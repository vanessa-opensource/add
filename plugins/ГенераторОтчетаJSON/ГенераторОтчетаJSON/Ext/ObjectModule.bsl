
// { Plugin interface
Функция ОписаниеПлагина(КонтекстЯдра, ВозможныеТипыПлагинов) Экспорт
	Результат = Новый Структура;
Результат.Вставить("Тип", ВозможныеТипыПлагинов.ГенераторОтчета);
	Результат.Вставить("Идентификатор", Метаданные().Имя);
	Результат.Вставить("Представление", "Отчет о тестировании в формате JSON");
	Результат.Вставить("ПотоковыйВывод", Ложь); // TODO Нужно или нет?
	Результат.Вставить("ФормироватьСводныйФайл", Ложь); // TODO Нужно или нет?

	Возврат Новый ФиксированнаяСтруктура(Результат);
КонецФункции

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
КонецПроцедуры
// } Plugin interface

// { Report generator interface
Функция СоздатьОтчет(КонтекстЯдра, РезультатыТестирования) Экспорт
	ПостроительДереваТестов = КонтекстЯдра.Плагин("ПостроительДереваТестов");
	ЭтотОбъект.ТипыУзловДереваТестов = ПостроительДереваТестов.ТипыУзловДереваТестов;
	ЭтотОбъект.СостоянияТестов = КонтекстЯдра.СостоянияТестов;
	Отчет = СоздатьОтчетНаСервере(РезультатыТестирования);

	Возврат Отчет;
КонецФункции

Функция СоздатьОтчетНаСервере(РезультатыТестирования) Экспорт

	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, РезультатыТестирования, , "ПреобразоватьВJson", ЭтотОбъект);

	СтрокаJSON = ЗаписьJSON.Закрыть();

	ЗаписьЖурналаРегистрации("VanessaAdd.ГенераторОтчетаJSON", УровеньЖурналаРегистрации.Предупреждение,,, СтрокаJSON);

	Результат = Новый ТекстовыйДокумент;
	Результат.ДобавитьСтроку(СтрокаJSON);

	Возврат Результат;
КонецФункции

#Если ТолстыйКлиентОбычноеПриложение Тогда
Процедура Показать(Отчет) Экспорт
	Отчет.Показать();
КонецПроцедуры
#КонецЕсли

Процедура Экспортировать(Отчет, ПолныйПутьФайла) Экспорт
	Отчет.Записать(ПолныйПутьФайла);
КонецПроцедуры

// Процедура ЗаписатьРезультатТестаНаСервере(Знач РезультатТеста, Знач ПолныйПутьФайла) Экспорт
Функция ПолучитьРезультатТестаНаСервере(Знач РезультатТеста) Экспорт

	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, РезультатТеста, , "ПреобразоватьВJson", ЭтотОбъект);

	СтрокаJSON = ЗаписьJSON.Закрыть();
	ЗаписьЖурналаРегистрации("VanessaAdd.ГенераторОтчетаJSON", УровеньЖурналаРегистрации.Предупреждение,,, СтрокаJSON);

	Результат = Новый ТекстовыйДокумент;
	Результат.ДобавитьСтроку(СтрокаJSON);

	Возврат Результат;

КонецФункции
// } Report generator interface

// { Helpers
Функция ПреобразоватьВJson(Свойство, Значение, ДополнительныеПараметры, Отказ) Экспорт
	Если ТипЗнч(Значение) = Тип("УникальныйИдентификатор") Тогда
		Возврат Строка(Значение);
	ИначеЕсли Свойство = "Родитель" Тогда
		Отказ = Истина;
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

// } Helpers
