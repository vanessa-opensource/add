
//////////////////////////////////////////////////////////////////////////
// Работа с командными файлами

//#Использовать tempfiles

Перем мЗаписьТекста;
Перем мПуть;

// { Plugin interface
Функция ОписаниеПлагина(ВозможныеТипыПлагинов) Экспорт
	Результат = Новый Структура;
	Результат.Вставить("Тип", ВозможныеТипыПлагинов.Утилита);
	Результат.Вставить("Идентификатор", Метаданные().Имя);
	Результат.Вставить("Представление", "КомандныйФайл");
	
	Возврат Новый ФиксированнаяСтруктура(Результат);
КонецФункции

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
КонецПроцедуры
// } Plugin interface

//{ API

Функция ОткрытьФайл(Знач Путь = "") Экспорт
	
	Если ПустаяСтрока(Путь) Тогда
		мПуть = ПолучитьИмяВременногоФайла(".bat");
		//мПуть = ВременныеФайлы.НовоеИмяФайла(".bat");
	Иначе
		мПуть = Путь;
	КонецЕсли;
	
	мЗаписьТекста = Новый ЗаписьТекста(мПуть, "cp866");
	
	Возврат мПуть;
	
КонецФункции

Процедура Добавить(Знач Команда) Экспорт
	ПроверитьЧтоФайлОткрыт();
	мЗаписьТекста.ЗаписатьСтроку(Команда);
КонецПроцедуры

Функция ВыполнитьКоманду() Экспорт
	
	ЗакрытьФайл();
	
	ПутьПакетногоФайла = мПуть;
	
	СтрокаЗапуска = "cmd.exe /C """ + ПутьПакетногоФайла + """";
	
	КодВозврата = "";
	ЗапуститьПриложение(СтрокаЗапуска,, Истина, КодВозврата);
	
	Возврат КодВозврата;

КонецФункции

Функция ЗакрытьФайл() Экспорт
	
	Если мЗаписьТекста <> Неопределено Тогда
		мЗаписьТекста.Закрыть();
		мЗаписьТекста = Неопределено;
	КонецЕсли;
	
	Возврат мПуть;
	
КонецФункции

//}

//{ helpers

Процедура ПроверитьЧтоФайлОткрыт()
	Если мЗаписьТекста = Неопределено Тогда
		ОткрытьФайл();
	КонецЕсли;
КонецПроцедуры

//}
